apply plugin: 'groovy'
apply plugin: 'com.ofg.uptodate'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

buildscript {
    repositories {
        mavenLocal()
        jcenter()
    }

    dependencies {
        classpath 'com.ofg:uptodate-gradle-plugin:1.1.0'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:+'
        if (project.hasProperty("coverage")) { classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:+' }
    }
}

description = 'Modifies our Wiremock based stub definitions to support the notion of Consumer Driven Contracts'
group = 'com.ofg'
version = '0.0.1'

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
}

ext {
    slf4jVersion = '1.7.7'
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.3.8'
    compile ('com.github.tomakehurst:wiremock:1.53') {
        exclude group: 'org.mortbay.jetty', module: 'servlet-api'
    }
    compile 'javax.servlet:javax.servlet-api:3.0.1'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.4.4'

    testCompile "org.slf4j:slf4j-api:$slf4jVersion"
    testCompile "org.slf4j:log4j-over-slf4j:$slf4jVersion"
    testCompile 'ch.qos.logback:logback-classic:1.1.2'
    testCompile 'org.spockframework:spock-core:0.7-groovy-2.0'
}

task sourcesJar(type: Jar, dependsOn:classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn:javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        code(MavenPublication) {
            from components.java

            artifact sourcesJar {
                classifier "sources"
            }

            artifact javadocJar {
                classifier "javadoc"
            }
        }
    }
}

bintray {
    user = project.properties['bintrayUser']
    key = project.properties['bintrayKey']
    publications = ['code']
    pkg {
        repo = 'com-blogspot-toomuchcoding'
        name = 'wiremock-placeholders'
        desc = '''Functionality of placeholders in the Wiremock stub definitions'''
        licenses = ['Apache-2.0']
        labels = ['wiremock']
    }
}